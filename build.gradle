plugins {
    id 'java-base'
    id 'jacoco'
    id 'com.github.jk1.dependency-license-report'
    id "com.github.ben-manes.versions"
    id "com.enonic.defaults"
    id 'biz.aQute.bnd.builder' apply false
}

ext {
    leafProjects = subprojects.findAll { p -> p.subprojects.empty }
    javaProjects = leafProjects - project( ':runtime' ) - project( ':docs' )
    mavenPublishProjects = leafProjects
}

allprojects {
    group = 'com.enonic.xp'

    repositories {
        mavenLocal()
        jcenter()
    }

    apply plugin: 'com.enonic.defaults'
    apply from: "$rootDir/gradle/versions.gradle"
}

configure( mavenPublishProjects ) {
    apply plugin: 'maven-publish'
}

configure( javaProjects ) {
    apply from: "$rootDir/gradle/java.gradle"
}

configure( rootProject ) {
    apply from: "$rootDir/gradle/coverage.gradle"
}

task ci {
    description = 'Builds and generates coverage report.'
    dependsOn = ['build', 'jacocoMergedReport']
}

def isNonStable = { String version ->
    return ['alpha', 'beta', '-rc', '-m', '-b'].any { it -> version.toLowerCase().contains(it) }
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
